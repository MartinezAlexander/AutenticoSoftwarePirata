import piratas.*
import misiones.*
import barcos.*
import ciudades.*
import souvenires.*

describe "Autentico Software pirata" {
	
	const jack = new Pirata(items=["mapa","oro","collar de perlas","mapa New York"], dinero=7, habilidad_principal="saqueos")
	const barbanegra = new Pirata(items=["brujula", "dos cuchillos", "un diente de oro","un loro"], dinero=8, habilidad_principal="espiar")
	const jones = new Pirata(items=["llave de cofre","brujula"], dinero=4, habilidad_principal="saqueos")
	const busqueda_del_teroso = new BusquedaDelTesoro()
	
	const new_york = new Ciudad(nombre="New York",souvenir = new Souvenir(item = "diamante", valor = 1), habitantes=[jack,barbanegra,jones])
	const un_barco = new Barco(tripulantes= [jack], mision = busqueda_del_teroso, capacidad = 3)
	
	const saqueo_a_ciudad = new Saqueo(lugar_a_saquear = new_york , cantidad_monedas_tope = 8)
	const saqueo_a_barco = new Saqueo(lugar_a_saquear = un_barco, cantidad_monedas_tope = 10)
	
	const perla_negra = new Barco(mision = saqueo_a_barco, capacidad = 3)
	
	const harry = new EspiaDeLaCorona(items=["mapa New York"], habilidad_principal = "correr", dinero = 5)
	
	test "jack puede ser reclutado por el perla negra" {
		assert.that(perla_negra.puedo_reclutarlo(jack))
	}
	
	test "el perla negra recluta a jack porque es util para la mision y tiene espacio" {
		perla_negra.reclutar_tripulante(jack)
		assert.equals(1,perla_negra.cantidad_de_tripulantes())
	}
	
	test "el perla negra no acepta a barbanegra por no ser experto en saqueos" {
		assert.throwsExceptionWithMessage("No puedo reclutarlo",{perla_negra.reclutar_tripulante(barbanegra)})
	}
	
	test "el perla negra recluta a jones y jack pero hecha a jones porque cambia de mision a saqueo de ciudad y no es util" {
		perla_negra.reclutar_tripulante(jack)
		perla_negra.reclutar_tripulante(jones)
		assert.equals(2,perla_negra.cantidad_de_tripulantes())
		perla_negra.cambiar_de_mision(saqueo_a_ciudad)
		assert.equals(1,perla_negra.cantidad_de_tripulantes())
	}
	
	test "el perla negra es temible si tiene a jack, jones, harry porque puede concretar el saqueo al barco" {
		perla_negra.reclutar_tripulante(jack)
		perla_negra.reclutar_tripulante(jones)
		harry.agregar_item("permiso de la corona")
		perla_negra.reclutar_tripulante(harry)
		assert.that(perla_negra.es_temible())
	}
	
	test "el perla negra no es temible porque harry no se anima a saquear el barco porque no tiene el permiso de la corona
			por ende no puede reclutarlo" {
		perla_negra.reclutar_tripulante(jack)
		perla_negra.reclutar_tripulante(jones)
		assert.equals(2,perla_negra.cantidad_de_tripulantes())
		assert.throwsExceptionWithMessage("No puedo reclutarlo", {perla_negra.reclutar_tripulante(harry)})
	}
	
	test "el perla negra es temible cambiando de mision y reclutando a jack, jones y harry
			porque puede concretar el saqueo a new york siempre y cuando jones adquiera un mapa de new york
			y harry tenga el permiso de la corono" {
		perla_negra.reclutar_tripulante(jack)
		jones.agregar_item("mapa New York")
		perla_negra.reclutar_tripulante(jones)
		harry.agregar_item("permiso de la corona")
		perla_negra.reclutar_tripulante(harry)
		assert.that(perla_negra.es_temible())
	}
	
}